/*
 * Movico
 * https://github.com/d-plaindoux/movico
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */

module Data.Number

from Data.Internal import internal
from Data.Native import native
from Data.Boolean import bool
from Data.Types import *

class number this:native {
    (==)     : number -> bool
    (!=)     : number -> bool
    (<=)     : number -> bool
    (<<)     : number -> bool
    (>>)     : number -> bool
    (>=)     : number -> bool
    (+)      : number -> number
    (-)      : number -> number
    (*)      : number -> number
    (/)      : number -> number
    hash     : number
    toString : string
    
    hashable   : hashable[number]
    comparable : comparable[number]
} {
    def (==) n = internal "generic.==" apply self n true false
    def (!=) n = (self == n) not
    def (<=) n = (self == n) || (self << n)
    def (<<) n = internal "generic.<<" apply self n true false
    def (>>) n = n <= self
    def (>=) n = n << self
    def (+) n = internal "number.+" apply self n
    def (-) n = internal "number.-" apply self n
    def (*) n = internal "number.*" apply self n
    def (/) n = internal "number./" apply self n
    def hash = self
    def toString = internal "number.toString" apply self
    
    def hashable = hashable self self.hash
    def comparable = comparable self self.(==) self.(!=) self.(>>) self.(<<)
}
