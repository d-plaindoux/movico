/*
 * Movico
 * https://github.com/d-plaindoux/movico
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Data.Try

from Data.Boolean import bool
from Data.String import string
 
type Try[a] {
    model Success { _ : a }
    model Failure { _ : string }
}

class try[a] this:Try[a] {    
    map         : [b] (a -> b) -> try[b]
    flatmap     : [b] (a -> try[b]) -> try[b]
    fold        : [b] (a -> b) -> (string -> b) -> b
    filter      : (a -> bool) -> try[a]
    recoverWith : (string -> a) -> a
} {
    def Failure.fold _ f = f this._
    def Success.fold s _ = s this._

    def Failure.map f = failure this._
    def Success.map f = success $ f this._

    def Failure.flatmap f = failure this._
    def Success.flatmap f = f this._

    def Failure.filter _ = self
    def Success.filter p = 
        let filtered = p this._ in
            filtered fold self $ failure "Filter fails"

    def Failure.recoverWith a = a this._
    def Success.recoverWith _ = this._
}

def success : [a] a -> try[a] 
            = fun a -> try $ Success a

def failure : [a] string -> try[a] 
            = fun a -> try $ Failure a
