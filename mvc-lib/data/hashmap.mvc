/*
 * Movico
 * https://github.com/d-plaindoux/movico
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Data.Hashmap
 
from Data.Boolean import bool
from Data.List import list
from Data.Number import number
from Data.Option import option
from Data.Pair import Pair
from Data.Types import comparable hashables
from Data.Array import array
from Logic.Combinators import I

typedef Hashmap[k v] = array[list[(k,v)]] 

class hashmap[k v] this:Hashmap[k v] {
  unbox  : Hashmap[k v]
 
  empty  : bool
 
  find   : Comparable[k] -> option[v]
  add    : Comparable[k] -> v -> hashmap[k v]

  mapper : Map[k v]
} {
  def unbox = this

  def empty = 
      this foldR (e _ -> e empty) true

  def find c = 
      let index = c hashable hash % this.length in
      let retrieve = l -> l find (e -> c == e._1) map (e -> e._2) in
          this get index flatmap retrieve
      
  def add c v = 
      let index = c hashable hash % this.length in
      let cleanup = l -> l filter (e -> c != e._1) in
      let entries = this get index fold new$list cleanup in
          self $ this set index $ entries +: ((c unbox),v)          
          
  def mapper = Map self.find (c v -> self.add c v mapper)
}

def new$map = hashmap $ new$array 256
