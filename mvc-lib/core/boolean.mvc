/*
 * Movico
 * https://github.com/d-plaindoux/movico
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */

module Bool

type Bool {
    model True
    model False
}

class bool this:Bool {
    unbox    : Bool
    fold     : [a] a -> a -> a
    and      : bool -> bool
    or       : bool -> bool
    imply    : bool -> bool
    (&&)     : bool -> bool
    (||)     : bool -> bool
    (=>)     : bool -> bool
    not      : bool
    toString : string
} {
    def unbox = this

    def True.fold  t _ = t
    def False.fold _ f = f

    def and = self &&
    def or = self ||
    def imply = self =>
    
    def (&&) l = self fold l self
    def (||) l = self fold self l
    def (=>) l = self.not || l
    def not = self fold false true
}

def true  : bool = bool True
def false : bool = bool False
