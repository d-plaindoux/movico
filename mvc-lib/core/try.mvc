/*
 * Movico
 * https://github.com/d-plaindoux/movico
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Core.Try

from Core.Boolean import bool
from Core.String import string
 
type Try[a] {
    model Success { _ : a }
    model Failure { _ : string }
}

class try[a] this:Try[a] {    
    map         : [b] (a -> b) -> try[b]
    flatmap     : [b] (a -> try[b]) -> try[b]
    fold        : [b] (a -> b) -> (string -> b) -> b
    filter      : (a -> bool) -> try[a]
    recoverWith : (string -> a) -> a
} {
    def Failure[a].fold _ f = f this._
    def Success[a].fold s _ = s this._

    def Failure[a].map f = failure this._
    def Success[a].map f = success $ f this._

    def Failure[a].flatmap f = failure this._
    def Success[a].flatmap f = f this._

    def Failure[a].filter _ = self
    def Success[a].filter p = 
        let filtered = p this._ in
            filtered fold self $ failure "Filter fails"

    def Failure[a].recoverWith a = a this._
    def Success[a].recoverWith _ = this._
}

def success : [a] a -> try[a] 
            = fun a -> try $ Success a

def failure : [a] string -> try[a] 
            = fun a -> try $ Failure a
