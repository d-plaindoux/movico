/*
 * Movico
 * https://github.com/d-plaindoux/movico
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
 require boolean
 
 type Try [a] {
    model Success { _ : a }
    model Failure { _ : string }
}

class try[a] this:Try[a] {
    fold        : [b] (a -> b) -> (string -> b) -> b
    map         : [b] (a -> b) -> try[b]
    flatmap     : [b] (a -> try[b]) -> try[b]
    filter      : (a -> bool) -> try[a]
    recoverWith : a -> a
} {
    def Failure[a].fold _ f = f this._
    def Success[a].fold s _ = s this._

    def Failure[a].map f = try $ Failure this._
    def Success[a].map f = try $ Success $ f this._

    def Failure[a].flatmap f = try $ Failure this._
    def Success[a].flatmap f = f this._

    def Failure[a].filter _ = self
    def Success[a].filter p = 
        let filtered = p this._ in
            filtered fold self $ self (Failure "Filter fails")

    def Failure[a].recoverWith a = a
    def Success[a].recoverWith _ = this._
}
