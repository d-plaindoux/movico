--------------------------------------------------------------------------
 Thicket grammar definition   
--------------------------------------------------------------------------
 V4                                                             07/20/2015               
--------------------------------------------------------------------------

module ::= 
  "module" IDENT ("." IDENT)* import* entity*

--------------------------------------------------------------------------

import ::= 
  "from" IDENT ("." IDENT)* "import" ("*"|nameDef+)

--------------------------------------------------------------------------
         
entity ::= 
  typeDef | sortDef | objectDef | expressionDef | classDef

--------------------------------------------------------------------------

typeDef ::= 
  "typedef" ID generics "=" generics types

--------------------------------------------------------------------------

sortDef ::= 
  "type" ID generics "{" ("model" ID ("{" tparam* "}")?)+ "}"

--------------------------------------------------------------------------

objectDef ::= 
  "model" ID generics ("{" tparam* "}")?

--------------------------------------------------------------------------

expressionDef ::= 
  "def" "adapter"? nameDef (":" generics type)? "="
    ((ID (":" generics type)?)+ ->)? expr

--------------------------------------------------------------------------

tparam ::= 
  nameDef ":" generics type
    
type ::= 
  ID ("[" type+ "]")?
| "(" types ")" 

types ::= 
  type "," types
| type "->" types
| type

--------------------------------------------------------------------------

traitDef ::= 
  "trait" ID generics "{" ("+" type|tparam)* "}" "{" method* "}"

--------------------------------------------------------------------------

classDef ::= 
  "class" ID generics param "{" ("+" type|tparam)* "}" "{" method* "}"

--------------------------------------------------------------------------

methodDef ::= 
  "def" "adapter"? nameDef (":" generics type)? "=" expr

--------------------------------------------------------------------------

method ::= 
  "def" (ID ".")? nameDef (ID(":" generics type)?)* "=" expr
  
nameDef ::=
  (ID|"("OP")")

--------------------------------------------------------------------------

generics ::= 
  ("[" ID+ "]")?

--------------------------------------------------------------------------

exprs ::= 
  expr expr*

expr ::= 
  iexpr ("," iexpr)*

iexpr ::= 
  sexpr ("." ID)*

sexpr ::= 
  NUMBER
| STRING
| type "::" (ID|"("OP")")
| ID
| OP
| "$" exprs 
| "(" exprs ")"
| "{" exprs "}"
| "<" ID attr* ">" exprs "</" ID? ">" 
| "<" ID attr* "/>"
| "for" (ID "<-" sexpr)+ ("if" exprs)* "yield" exprs
| "let" ID (":" generics type)? "=" exprs "in" exprs
| type "::" ID
| (ID (":" generics type)?)+ -> exprs
| "new" exprs "with" (ID "=" sexpr)+
            
attr ::= 
  ID "=" expr

--------------------------------------------------------------------------

param ::= 
  ID ":" type

--------------------------------------------------------------------------

comment ::= 
  "//" line | "/*" line* "*/"

--------------------------------------------------------------------------
