/*
 * Thicket
 * https://github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Data.Math.Monoid
 
from Data.Pair import Pair
from Data.Number import number
from Data.Boolean import bool

model Monoid[k] {
    append : k -> k -> k
    zero   : k
}

// -------------------------
// Monoid laws
// -------------------------
// m apply a (m apply b c) = m apply (m apply a b) c
// m apply a (m neutral)   = a
// m apply (m neutral) a   = a

class monoidLaws[k] this:(Monoid[k],(k->k->bool)) {
    verifyLaws    : k -> k -> k -> bool
    associativity : k -> k -> k -> bool
    neutral_left  : k -> bool
    neutral_right : k -> bool
} {
    def verifyLaws a b c =
          self associativity a b c and 
        $ self neutral_left a and 
        $ self neutral_right a
    
    def associativity a b c = 
        this._2 { this._1 append a (this._1 append b c) } 
                { this._1 append (this._1 append a b) c }
        
    def neutral_left a = 
        this._2 { this._1 append a (this._1.zero) } a
        
    def neutral_right a = 
        this._2 { this._1 append (this._1.zero) a } a
}

/* Some Monoid or not

monoidLaws (Monoid number::(+) 0),number::(==) verifyLaws 4 2 3 == true
monoidLaws (Monoid number::(*) 1),number::(==) verifyLaws 4 2 3 == true
monoidLaws (Monoid number::(-) 0),number::(==) verifyLaws 4 2 3 == false // no Neutral element
monoidLaws (Monoid number::(/) 1),number::(==) verifyLaws 4 2 3 == false // no Associativity

*/
