/*
 * Movico
 * https://github.com/d-plaindoux/movico
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Examples.Nat
 
from Data.Number import number
from Data.String import string
 
type Nat {
    model Zero
    model Succ { _ : Nat }
}
 
class nat this:Nat {
    unbox : Nat
    fold  : [a] a -> (nat -> a) -> a
    (+)   : nat -> nat
} {
    def unbox = this
    
    def Zero.fold z s = z
    def Succ.fold z s = s (self this._)
    
    def Zero.(+) p = p
    def Succ.(+) p = self $ Succ $ self this._ + p unbox
}
 
class natInterp this:nat {
    toNumber : number
    toString : string
} {
    def toNumber = 
        let onSucc = fun p -> 1 + $ self p toNumber in
            this fold 0 onSucc
            
    def toString = 
        let onSucc = fun p -> "Succ(" + (self p toString) + ")" in
            this fold "Zero" onSucc
}

def zero : nat = nat Zero
def one  : nat = nat $ Succ Zero
