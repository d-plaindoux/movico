<html>
<head>
  <script src="../../build/movico-lang.js"></script>
  <script type="text/movico">
    // --------------------------------------------------------
    // Core library
    // --------------------------------------------------------
    
    model native
    
    class string this:native {
      toDom : dom
    }{}
    
    class dom this:native {
	  appendChild : dom -> dom
      (<<) : dom -> dom
    }{
	  def (<<) = self appendChild
	}
    
    class document this:unit {
      elementById : string -> dom
	  (@) : string -> dom
    }{
	  def (@) = self elementById
	}
    
	// List definition & implementation
	
    type List[a] {
      model Nil
      model Cons {
	    head : a
        tail : List[a]
      }
    }
    
    class list[a] this:List[a] {
	  unbox : List[a]
      (:+)  : a -> list[a]
      foldR : [b] (a -> b -> b) -> b -> b
	  map   : [b] (a -> b) -> list[b]
    } {
	  def unbox = this
	  
	  def Nil[a].(:+) a = self $ Cons a this
	  def Cons[a].(:+) a = self $ Cons this.head $ self this.tail :+ a unbox
    
      def Nil[a].foldR  _ b = b
      def Cons[a].foldR f b = self (this tail) foldR f (f this.head b)
	  
	  def Nil[a].map f = list Nil
	  def Cons[a].map f = list $ Cons (f this.head) $ self this.tail map f unbox  
    }
	
    // --------------------------------------------------------
	// Todo simple example
    // --------------------------------------------------------
    
    model Todo {
      _ : string
    }
    
    class todo this:Todo {
      toDom : dom
    } {
      def toDom = <li><i> this._.toDom </i></li>
    }
  </script>

  <script type="text/movico">
    let title = document() @ "title" in 
        title << $ "Things to be done:" toDom
  </script>

  <script type="text/movico">
    let texts = list Nil :+ "Something #1" :+ "Something #2" :+ "Something #3" in
	let todos = texts map (fun s -> Todo s) in
	let node = document() @ "todo" in
        todos foldR (fun t h -> h << $ todo t toDom) node
   </script>
</head>
	
<body onload="require('movico').compile().execute()">
	<b id="title"></b>
	<ul id="todo"></ul>
</body>
</html>

