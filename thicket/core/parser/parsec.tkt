/*
 * Thicket
 * https:=github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015-2016 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
 /*
  * Parsec: Direct Style Monadic Parser Combinators For The Real World
  * 
  * http://research.microsoft.com/en-us/um/people/daan/download/papers/parsec-paper.pdf
  */
 
module Parsec.Combinator
 
import Boot.Core
 
type Response[a] {
    model Consumed { _: Reply[a] }
    model Empty    { _: Reply[a] }
}
 
type Reply[a] {
    model Ok { value:a input:sequence[char] offset:number }
    model Error
} 
 
//
// Fold functionalities
//

class response[a] this:Response[a] {
    with Monadic[Response,a]
    
    fold : [b] (Reply[a] -> b) -> (Reply[a] -> b) -> b
} {
    def Consumed.map f = Consumed $ reply this._ map f
    def Empty.map    f = Empty    $ reply this._ map f
    
    // def Consumed.flatmap f = Consumed $ reply this._ map f
    // def Empty.flatmap    f = Empty $ reply this._ map f

    def Consumed.fold c _ = c this._
    def Empty.fold    _ e = e this._
}

class reply[a] this:Reply[a] {
    with Monadic[Reply,a]

    fold : [b] (a -> sequence[char] -> number -> b) -> b -> b
} {
    def Ok.map    f = Ok (f this.value) this.input this.offset
    def Error.map _ = Error
    
    def Ok.fold    o _ = o this.value this.input this.offset
    def Error.fold _ e = e
}

//
// Parser trait definition
//

trait Parser[a] {
    with Monadic[Parser,a]
    
    parse : sequence[char] -> number -> Response[a]

    (<|>) : Parser[a] -> Parser[a]
    (<*>) : [b] Parser[(a -> b)] -> Parser[b]
} {
    def map     f = parser $ s n -> response (self parse s n) map     f
    // def flatmap f = parser $ s n -> response (self parse s n) flatmap f
    def filter  p = parser $ s n -> response (self parse s n) filter  p

    def (<|>) = choice self
}

//
// Parser class definition
//

class parser[a] this:sequence[char] -> number -> Response[a] {
    with Parser[a]
} {
    def parse = this
}

//
// Basic parsers
//

def return : [a] a -> Parser[a] = a -> {
    parser $ input index -> 
        Empty $ Ok a input index
}

def satisfy : (char -> bool) -> Parser[char] = test -> {
    parser $ input index ->
        input isEmpty fold
            { 
               (for c <- { input get index } 
                if (test c) 
                yield Consumed (Ok c input $ index++)
               ) orElse (Empty Error) 
            }
            { Empty Error }

}

def character : char -> Parser[char] = c -> {
    satisfy (c==)
}

def letter : Parser[char] = {
    satisfy (c -> ('a' <= c && $ c <= 'z') || ('A' <= c && $ c <= 'Z'))
}

def digit : Parser[char] = {
    satisfy (c -> '0' <= c && $ c <= '9')
}

def choice : [a] Parser[a] -> Parser[a] -> Parser[a] = p q -> {
    parser $ input index ->
        response (p parse input index) fold 
            Consumed 
            (r -> reply r fold 
                (a input index -> 
                    response (q parse input index) fold
                        Consumed
                        (_ -> Empty r)
                )
                (q parse input index)
            )
}

// 
// Additional Parsers
//