/*
 * Thicket
 * https://github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Data.Char

from System.Internal import internal
from Data.Native import native
from Data.Number import number
from Data.String import string, string2hashable
from Data.Boolean import true,false
from Data.Comparable import Comparable,comparable
from Data.Hashable import Hashable
from Data.Matcher import Matcher
from Data.Additionable import Additionable

from Lang.Switch import switch

class char this:native {
    with comparable[char]
    with Matcher[char]
    with Additionable[char]

    toString : string
} {
    def unbox = self

    def (==) n = internal "generic.==" self n true false
    def (<?) n = internal "generic.<" self n true false
    
    def (+) n = char $ internal "char.+" self n
    
    def toString = string this
    
    def match = switch self
}

def eol : char = '\n'

/**
 * Adapters
 */
 
def adapter char2string : char -> string = char::toString

def adapter char2hashable : char -> Hashable[char] = c -> {
    Hashable c $ string2hashable (char2string c) hash
}
    
def adapter char2comparable : char -> Comparable[char] = s -> {
    Comparable s s.(==) s.(!=) s.(=>) s.(<=) s.(?>) s.(<?) (char2hashable s)
}