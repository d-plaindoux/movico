/*
 * Thicket
 * https://github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */

module Example.TodoMVC.Views

from Client.Document.Renderer import *
from Client.Document import *
from Client.Event.Mouse import *
from Client.Event.Key import *

from Lang.Do import do
from Lang.When import when

from Logic.Combinators import combinators

from Data.Unit import unit
from Data.String import string
from Data.Number import number
from Data.Option import *

from Example.TodoMVC.Models import *
from Example.TodoMVC.Application import Application

// ------------------------------------------------------------------

class todoViews this:Application {
    todoStatusView      : Todo -> dom
    todoEditView        : Todo -> dom
    todoView            : Todo -> dom
    
    viewItems           : dom 
    createView          : dom
    filtersView         : dom
    clearCompletedView  : dom
    toggleAllView       : dom
    footerView          : dom
    todoListView        : dom
} {
    def todoStatusView t = 
        <input type="checkbox" class="toggle"
               checked if t.completed />
        onMouseEvent MouseClick $ _ -> this action toggle t.id
        
    def todoEditView t =
        <li id=("todo_" + t.id)
            class="completee" if t.completed>
          { <input class="edit" value=t.title autofocus/>             
            onKeyEvent KeyPress $ d n -> 
                when (n == standardKeys.CR) 
                then { d value fold this $ this action change t.id } 
                else this
          }
        </li>
        
    def todoView t =
        <li id=("todo_" + t.id) class=(when t.completed then "completed" else "")>
            <div class="view">  
                {self todoStatusView t}
                <label> t.title </label>
                {<button id=("todo_destroy_" + t.id) class="destroy"/>
                 onMouseEvent MouseClick $ _ -> this action remove t.id}
            </div>
        </li>
        onMouseEvent MouseDblClick $ _ -> this action edit t.id

    def viewItems = 
        let r = this.context.todosLeft in
            <span id="todo-count">
                <strong> r " " </strong> 
                { when (r <? 2) then "item left" else "items left" }
            </span>

    def filtersView =
        <ul id="filters">
            <li>  
                { <a href="#/" 
                     class="completed" if (this.context.filter == all)>
                     "All" 
                  </a>
                   onMouseEvent MouseClick $ _ -> this action filtering all }
            </li>
            <li>  
                { <a href="#/active" 
                     class="selected" if (this.context.filter == active)> 
                     "Active" 
                  </a>
                  onMouseEvent MouseClick $ _ -> this action filtering active }
            </li>
            <li>  
                { <a href="#/completed" 
                     class="selected" if (this.context.filter == completed)> 
                    "Completed" 
                  </a>
                  onMouseEvent MouseClick $ _ -> this action filtering completed }
            </li>
        </ul>

    def createView = 
        <input id="new-todo" placeholder="What needs to be done?" autofocus/>
        onKeyEvent KeyPress $ d n -> 
            when (n == standardKeys.CR) 
            then { d value fold this $ this action add } 
            else this

    def clearCompletedView =
        <button id="clear-completed"> "Clear completed" </button>            
        onMouseEvent MouseClick $ _ -> this action clearCompleted ()
        
    def toggleAllView = 
        <input id="toggle-all" type="checkbox"
               checked if this.context.completed
               class="hidden" if (this.context.length == 0) />
        onMouseEvent MouseClick $ _ -> this action completeAll ()
    
    def footerView = 
        when (this context length == 0)
        then <footer id="footer" class="hidden"/>
        else <footer id="footer">
                (self viewItems)
                (self filtersView)
                (self clearCompletedView)
             </footer>
             
    def todoListView =
        let todolist = <ul id="todo-list"/> in
            do 
            { this context todos map $ combinators.B (todolist addChild) $ t -> 
                    when (t.id == this.context.edited) 
                    then {self todoEditView t}
                    else {self todoView t} 
            }
            return todolist
}

// ------------------------------------------------------------------
// Rendering function
// ------------------------------------------------------------------

def renderAll : Application -> Renderer = a -> {
    let todoViews = todoViews a in
        documentRenderer 
            <~ { todoViews createView    }
            <~ { todoViews todoListView  }
            <~ { todoViews footerView    }
            <~ { todoViews toggleAllView }
}

def render : Application -> Application = application -> {
    do
    { renderAll application }
    return application
}
